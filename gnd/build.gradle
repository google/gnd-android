/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'
apply from: '../config/checkstyle/checkstyle.gradle'
apply from: '../config/lint/lint.gradle'
apply from: '../config/pmd/pmd.gradle'
apply from: '../config/spotbugs/spotbugs.gradle'

project.ext {
    autoDisposeVersion = "1.4.0"
    autoValueVersion = "1.6.3"
    butterKnifeVersion = "10.2.1"
    daggerVersion = "2.27"
    firebaseCoreVersion = "17.2.3"
    gmsVersion = "17.0.0"
    lifecycleVersion = "2.2.0"
    navigationVersion = "1.0.0"
    roomVersion = "2.2.5"
    rxBindingVersion = "2.2.0"
    workVersion = "2.3.4"
}

// Load secrets.properties
def secretsFile = file('secrets.properties')
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}
def googleMapsApiKey = secrets.getProperty('GOOGLE_MAPS_API_KEY', '')

android {
    compileSdkVersion 28
    defaultConfig {
        // https://developer.android.com/about/dashboards/index.html for reference.
        applicationId "com.google.android.gnd"
        minSdkVersion 16
        // Switching targetSdkVersion to 27 or 28 causes the app to hang on map drags; diagnose and
        // fix before increasing this value.
        targetSdkVersion 26
        versionCode gitVersioner.versionCode
        versionName gitVersioner.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        // For rendering vector map markers.
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("string", "GOOGLE_MAPS_API_KEY", googleMapsApiKey)
        }
        debug {
            resValue("string", "GOOGLE_MAPS_API_KEY", googleMapsApiKey)
            FirebasePerformance {
                instrumentationEnabled false
            }
        }
    }

    // Keep the following configuration in order to target Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    viewBinding {
        enabled = true
    }
    sourceSets {
        test.manifest.srcFile "src/test/AndroidManifest.xml"
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':android-gmaps-addons')

    implementation 'androidx.multidex:multidex:2.0.1'

    // Stetho - A debug bridge for Android applications
    // Enables network and database debugging for the app under chrome://inspect in Chrome desktop browser.
    // http://facebook.github.io/stetho/
    implementation 'com.facebook.stetho:stetho:1.5.1'

    // Android legacy support Libraries.
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'

    // UI widgets.
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Support for Java streams.
    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.1'

    // Google Play Services.
    implementation "com.google.android.gms:play-services-auth:$project.gmsVersion"
    implementation "com.google.android.gms:play-services-maps:$project.gmsVersion"
    implementation "com.google.android.gms:play-services-location:$project.gmsVersion"

    // GeoJSON support
    implementation "com.google.maps.android:android-maps-utils:1.0.1"

    // Firebase and related libraries.
    implementation "com.google.firebase:firebase-analytics:$project.firebaseCoreVersion"
    implementation "com.google.firebase:firebase-core:$project.firebaseCoreVersion"
    implementation 'com.google.firebase:firebase-firestore:21.4.1'
    implementation 'com.google.firebase:firebase-auth:19.3.0'
    implementation 'com.google.firebase:firebase-perf:19.0.5'
    implementation 'com.github.FrangSierra:RxFirebase:1.5.7'
    implementation 'com.google.firebase:firebase-storage:19.1.1'

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Dagger
    implementation "com.google.dagger:dagger:$project.daggerVersion"
    implementation "com.google.dagger:dagger-android:$project.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$project.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$project.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$project.daggerVersion"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$project.daggerVersion"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$project.daggerVersion"

    // ButterKnife
    implementation "com.jakewharton:butterknife:$project.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$project.butterKnifeVersion"

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.jakewharton.rxbinding2:rxbinding:$project.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$project.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$project.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:$project.rxBindingVersion"
    implementation 'com.akaita.java:rxjava2-debug:1.4.0'

    // Android Arch Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime:$project.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$project.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$project.lifecycleVersion"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$project.lifecycleVersion"

    // Android Navigation
    implementation "android.arch.navigation:navigation-fragment:$project.navigationVersion"
    implementation "android.arch.navigation:navigation-ui:$project.navigationVersion"

    // Auto-value
    compileOnly "com.google.auto.value:auto-value-annotations:$project.autoValueVersion"
    annotationProcessor "com.google.auto.value:auto-value:$project.autoValueVersion"

    // Guava
    implementation 'com.google.guava:guava:27.0.1-android'

    // Picasso: Image downloading and caching library
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    implementation "androidx.room:room-guava:$roomVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"

    // WorkManager
    implementation "androidx.work:work-runtime:$workVersion"
    implementation "androidx.work:work-rxjava2:$workVersion"
    androidTestImplementation "androidx.work:work-testing:$workVersion"

    implementation "com.uber.autodispose:autodispose-android:$project.autoDisposeVersion"
    implementation "com.uber.autodispose:autodispose-android-archcomponents:$project.autoDisposeVersion"

    // Apache Commons IO
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation 'commons-io:commons-io:2.6'

    // Testing
    testImplementation 'junit:junit:4.13'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'android.arch.core:core-testing:1.1.1'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

apply plugin: 'androidx.navigation.safeargs'

// This must be last to avoid dependency collisions.
// https://developers.google.com/android/guides/google-services-plugin#introduction
apply plugin: 'com.google.gms.google-services'
